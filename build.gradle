import aQute.bnd.osgi.Processor

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.3.0'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"    
}

apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'maven-publish'

// The builder plugin doesn't provide access to the bnd properties.
// Provide workaround as bnd.getProperty(...)
ext.set("bnd", new Processor());
bnd.setProperties(project.file('bnd.bnd'))

group = 'jdrupes'
version = bnd.getProperty("base_version") + "-SNAPSHOT"

repositories {
    jcenter()
}

dependencies {
    compileOnly 'org.osgi:org.osgi.annotation:6.0.0'
    testCompile 'junit:junit:4.12'
}

// Configure sensible layout
sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'resources'
        }
    }
}

jar {
    
    into('META-INF') {
        from 'resources/META-INF'
    }
    
}

task javadocs(type: Javadoc) {
    classpath = project.sourceSets.main.compileClasspath
    source = sourceSets.main.allJava
    destinationDir = file("../org.jdrupes.httpcodec.gh-pages/javadoc")
    options.addStringOption("bottom", file("misc/javadoc.bottom.txt").text)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom.withXml {
                asNode().appendNode('name', bnd.getProperty('Bundle-Name'))
            }
            from components.java
        }
    }
}

bintray {
    user = project.properties['bintrayUser'] ?: "nouser"
    key = project.properties['bintrayApiKey'] ?: "nokey"
    publications = ['mavenJava']
    pkg {
        repo = project.group
        name = project.name
        version {
            name = project.version
        }
    }
}

//// Publish to local maven repository using bnd
//import aQute.bnd.repository.maven.provider.MavenBndRepository
//import aQute.bnd.service.RepositoryPlugin.PutOptions
//
//task('release') {
//    description 'Release the project to the release repository.'
//    dependsOn assemble
//    group 'release'
//
//    doLast {        
//        new Processor().withCloseable { processor ->
//            processor.setProperties(project.file('bnd.bnd'))
//            def repo = processor.getPlugin(MavenBndRepository.class);
//            def opts = new PutOptions()
//            opts.context = processor
//            project.jar.archivePath.withInputStream { stream ->
//                println repo.put(stream, opts).artifact
//            }
//        }
//    }
//}
